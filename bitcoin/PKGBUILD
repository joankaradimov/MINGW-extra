# Maintainer: Joan Karadimov <joan.karadimopv@gmail.com>

_realname=bitcoin
pkgbase=mingw-w64-${_realname}
pkgname=(# ${MINGW_PACKAGE_PREFIX}-bitcoin-daemon
         ${MINGW_PACKAGE_PREFIX}-bitcoin-cli
         ${MINGW_PACKAGE_PREFIX}-bitcoin-qt
         ${MINGW_PACKAGE_PREFIX}-bitcoin-tx)
pkgver=0.21.0
pkgrel=1
arch=('any')
url="https://www.bitcoin.org/"
makedepends=(${MINGW_PACKAGE_PREFIX}-boost
             ${MINGW_PACKAGE_PREFIX}-libevent
             ${MINGW_PACKAGE_PREFIX}-qt5
             ${MINGW_PACKAGE_PREFIX}-qrencode
             ${MINGW_PACKAGE_PREFIX}-miniupnpc
             ${MINGW_PACKAGE_PREFIX}-protobuf
             ${MINGW_PACKAGE_PREFIX}-libsodium
             ${MINGW_PACKAGE_PREFIX}-zeromq)
license=('MIT')
source=("bitcoin-$pkgver.tar.gz::https://github.com/bitcoin/bitcoin/archive/v$pkgver.tar.gz"
        'https://download.oracle.com/berkeley-db/db-4.8.30.NC.tar.gz'
        'dependency-fixes.patch'
	'gcc-segfault-workaround.patch')
sha256sums=('1a91202c62ee49fb64d57a52b8d6d01cd392fffcbef257b573800f9289655f37'
            'SKIP'
            'SKIP'
	    'SKIP')
b2sums=('d04268d8ba03398eddbd0b37a53a4ba26f601e0c30bee6da24d3e489276c1d4b2486303348ab981d43c5d55d2e4b71a59a22f881bd31c347aa30ff07ccffed77'
        'SKIP'
        'SKIP'
	'SKIP')

prepare() {
  cd "$_realname-$pkgver"
  patch -p1 -i ${srcdir}/dependency-fixes.patch
  patch -p1 -i ${srcdir}/gcc-segfault-workaround.patch
  autoreconf -fi
}

build() {
  pushd db-4.8.30.NC/build_unix
  ../dist/configure --enable-mingw --enable-cxx --disable-shared --disable-replication
  make clean
  make
  popd

  cd $_realname-$pkgver
  PKG_CONFIG="/usr/bin/pkgconf.exe" LIBS="$LIBS -lbcrypt" LDFLAGS="-L$srcdir/db-4.8.30.NC/build_unix $LDFLAGS" CPPFLAGS="-I$srcdir/db-4.8.30.NC/build_unix $CPPFLAGS" ./configure \
    --prefix=${MINGW_PREFIX} \
    --with-gui=qt5 \
    --disable-tests \
    --with-boost=${MINGW_PREFIX}/include/boost \
    --with-boost-libdir=${MINGW_PREFIX}/bin
  make clean
  make
}

_package_bitcoin-qt() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - Qt"
  depends=(${MINGW_PACKAGE_PREFIX}-boost
           ${MINGW_PACKAGE_PREFIX}-libevent
           ${MINGW_PACKAGE_PREFIX}-qt5
           ${MINGW_PACKAGE_PREFIX}-miniupnpc
           ${MINGW_PACKAGE_PREFIX}-qrencode
           ${MINGW_PACKAGE_PREFIX}-protobuf
           ${MINGW_PACKAGE_PREFIX}-zeromq
           ${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme)

  cd $_realname-$pkgver
  install -Dm755 src/qt/bitcoin-qt.exe "${pkgdir}${MINGW_PREFIX}"/bin/bitcoin-qt.exe
  install -Dm644 src/qt/res/src/bitcoin.svg "${pkgdir}${MINGW_PREFIX}"/share/icons/hicolor/scalable/apps/bitcoin-qt.svg

  for i in 16 32 64 128 256; do
    install -Dm644 share/pixmaps/bitcoin${i}.png "${pkgdir}${MINGW_PREFIX}"/share/icons/hicolor/${i}x${i}/apps/bitcoin-qt.png
  done

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/COPYING"
}

# _package_bitcoin-daemon() {
#   pkgdesc="Bitcoin is a peer-to-peer network based digital currency - daemon"
#   depends=(${MINGW_PACKAGE_PREFIX}-boost
#            ${MINGW_PACKAGE_PREFIX}-db
#            ${MINGW_PACKAGE_PREFIX}-libevent
#            ${MINGW_PACKAGE_PREFIX}-miniupnpc
#            ${MINGW_PACKAGE_PREFIX}-sqlite
#            ${MINGW_PACKAGE_PREFIX}-zeromq)
#   backup=('etc/bitcoin/bitcoin.conf')
#
#   cd $_realname-$pkgver
#   install -Dm755 src/bitcoind.exe "${pkgdir}${MINGW_PREFIX}"/bin/bitcoind.exe
#   install -Dm644 contrib/bitcoind.bash-completion "${pkgdir}${MINGW_PREFIX}"/share/bash-completion/completions/bitcoind
#   install -Dm644 doc/man/bitcoind.1 "${pkgdir}${MINGW_PREFIX}"/share/man/man1/bitcoind.1
#   install -Dm644 contrib/init/bitcoind.service "${pkgdir}${MINGW_PREFIX}/lib/systemd/system/bitcoind.service"
#   install -Dm644 "$srcdir/bitcoin.sysusers" "${pkgdir}${MINGW_PREFIX}/lib/sysusers.d/bitcoin.conf"
#   install -Dm644 "$srcdir/bitcoin.tmpfiles" "${pkgdir}${MINGW_PREFIX}/lib/tmpfiles.d/bitcoin.conf"
#   install -Dm644 share/examples/bitcoin.conf "${pkgdir}${MINGW_PREFIX}/etc/bitcoin/bitcoin.conf"
#
#   install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/COPYING"
# }

_package_bitcoin-cli() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - RPC client"
  depends=(${MINGW_PACKAGE_PREFIX}-boost ${MINGW_PACKAGE_PREFIX}-libevent)

  cd $_realname-$pkgver
  install -Dm755 src/bitcoin-cli.exe "${pkgdir}${MINGW_PREFIX}"/bin/bitcoin-cli.exe
  install -Dm644 contrib/bitcoin-cli.bash-completion "${pkgdir}${MINGW_PREFIX}"/share/bash-completion/completions/bitcoin-cli
  install -Dm644 doc/man/bitcoin-cli.1 "${pkgdir}${MINGW_PREFIX}"/share/man/man1/bitcoin-cli.1

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/$pkgname/COPYING"
}

_package_bitcoin-tx() {
  pkgdesc="Bitcoin is a peer-to-peer network based digital currency - Transaction tool"
  depends=(${MINGW_PACKAGE_PREFIX}-boost ${MINGW_PACKAGE_PREFIX}-openssl)

  cd $_realname-$pkgver
  install -Dm755 src/bitcoin-tx.exe "${pkgdir}${MINGW_PREFIX}"/bin/bitcoin-tx.exe
  install -Dm644 contrib/bitcoin-tx.bash-completion "${pkgdir}${MINGW_PREFIX}"/share/bash-completion/completions/bitcoin-tx
  install -Dm644 doc/man/bitcoin-tx.1 "${pkgdir}${MINGW_PREFIX}"/share/man/man1/bitcoin-tx.1

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/$pkgname/COPYING"
}

package_mingw-w64-i686-bitcoin-qt()   { _package_bitcoin-qt; }
package_mingw-w64-x86_64-bitcoin-qt() { _package_bitcoin-qt; }
package_mingw-w64-i686-bitcoin-cli()   { _package_bitcoin-cli; }
package_mingw-w64-x86_64-bitcoin-cli() { _package_bitcoin-cli; }
package_mingw-w64-i686-bitcoin-tx()   { _package_bitcoin-tx; }
package_mingw-w64-x86_64-bitcoin-tx() { _package_bitcoin-tx; }

# vim:set ts=2 sw=2 et: