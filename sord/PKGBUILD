# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>

_realname=sord
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.16.8
pkgrel=1
pkgdesc="A C library for storing RDF data in memory"
arch=('any')
url="http://drobilla.net/software/sord"
license=('custom:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-serd")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             'python')
source=("http://download.drobilla.net/${_realname}-${pkgver}.tar.bz2")
sha256sums=('7c289d2eaabf82fa6ac219107ce632d704672dcfb966e1a7ff0bbc4ce93f5e14')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  /usr/bin/python ./waf configure --prefix=${MINGW_PREFIX}
  /usr/bin/python ./waf -v
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python ./waf -v install --destdir=${pkgdir}
  install -Dm644 COPYING "${pkgdir}/${MINGW_PREFIX}/share/licenses/$_realname/LICENSE"
  mv "${pkgdir}/${MINGW_PREFIX}/lib/"*dll "${pkgdir}/${MINGW_PREFIX}/bin"
}
