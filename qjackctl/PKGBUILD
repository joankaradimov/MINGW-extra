# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>

_realname=qjackctl
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.5
pkgrel=1
pkgdesc="A simple Qt application to control the JACK sound server"
url="https://qjackctl.sourceforge.io"
arch=('any')
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-qt5-base"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-portaudio"
         "${MINGW_PACKAGE_PREFIX}-jack2")
makedepends=('cmake' "${MINGW_PACKAGE_PREFIX}-qt5-tools")
source=("https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.gz"
        "install-targets.patch")
sha512sums=('2edceefd30a57b3b2277db271dd95c2b47b4a85106762eb3149dff94328265c096c5a26d301bd0edcd8f5d948dfc24ff2d3da532ac297121e4fee6982fb6ba56'
            'c3b3823c8864499cf422d024190eb96c683020cee769b7cb97898af32f932dbe1fb1b1fd6650260b94caff36b11cb9cb087ba5872bd2172437f2aac21e876aee')
b2sums=('3446b0cb5cd34541d5761f3f6e2361dcfb50656997c723b5972aa60466a1eb1a720fb5d97684079832705b084d8d1ca082f652145d51ff89aedfab71a90d5c5e'
        '51b99a633bec25e5e7437bc9f1cd23cd1e26c0e7aab8228607ede975db2ede9076a40713d891dbd67810d275af82b0df774cadbd99c80d9cc825f5a3488a49de')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/install-targets.patch
}


build() {
  cd "${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" cmake -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} -DCMAKE_INSTALL_DO_STRIP=1 -Wno-dev -B "build-${MINGW_CHOST}"
  ninja -C "build-${MINGW_CHOST}"
}

package() {
  cd "${_realname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja install -C "build-${MINGW_CHOST}"
  install -t "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}" -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
